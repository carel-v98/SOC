//Provides more detail to malicious emails
SecurityAlert
| project todynamic(Entities), TimeGenerated, AlertName
| mv-expand Entities
| project AlertName, TimeGenerated, NetworkMessageId = trim(@'^\["|"]$', tostring(Entities.NetworkMessageIds))
| join kind=inner (EmailUrlInfo) on NetworkMessageId
| project TimeGenerated, AlertName, Url, NetworkMessageId
| join kind=inner (EmailEvents) on NetworkMessageId
| project TimeGenerated, NetworkMessageId,Subject, Url, SenderFromAddress, SenderFromDomain, SenderIPv4, RecipientEmailAddress, AlertName

//Provides more detail to malicious emails
SecurityAlert
| project Entities, TimeGenerated, AlertName
| project AlertName, TimeGenerated, NetworkMessageId = tostring(parse_json(Entities)[1].NetworkMessageId), P2Sender = parse_json(Entities)[1].P2Sender,P2SenderName = parse_json(Entities)[1].P2SenderDisplayName, P1Sender = parse_json(Entities)[1].P1Sender
| join kind=inner (EmailUrlInfo) on NetworkMessageId
| project TimeGenerated, AlertName, Url, NetworkMessageId, P2Sender, P2SenderName, P1Sender
| join kind=inner (EmailEvents) on NetworkMessageId
//| where NetworkMessageId == ''
| project TimeGenerated, DeliveryAction, DeliveryLocation, Subject, Url, SenderFromAddress,P2Sender, P2SenderName, P1Sender, SenderFromDomain, SenderIPv4, RecipientEmailAddress, AlertName,  NetworkMessageId


